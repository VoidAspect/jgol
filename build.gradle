import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java-library'
    id 'idea'
}

group 'com.voidaspect'
version '0.10.0'

ext.moduleName = "${project.group}.${project.name}"

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': moduleName,
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': ZonedDateTime.now().format(DateTimeFormatter.ISO_ZONED_DATE_TIME),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'it.unimi.dsi:fastutil:8.4.4'

    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'org.mockito:mockito-core:3.5.7'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
    useJUnitPlatform()
    maxHeapSize = '3G'
}

